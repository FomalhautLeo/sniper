###############################################################################
# SNiPER CMAKE CONFIGURATION File for External Projects
# This file is generated by SNiPER CMake, and SHOULD NOT BE EDITED MANUALLY
#
# Exported Targets
#   SNiPER::SniperKernel    - the core component
#   SNiPER::SniperPythonLib - Python supports, such as PyDataStore
#   SNiPER::RootWriterLib   - the RootWriter library
#
# It also defines the following variables:
#   SNIPER_INCLUDE_DIR      - include directory
#   SNIPER_LIBRARY_DIR      - library directory
#
###############################################################################
@PACKAGE_INIT@

set_and_check(SNIPER_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")
set_and_check(SNIPER_LIBRARY_DIR "@PACKAGE_CMAKE_INSTALL_LIBDIR@")

include(CMakeFindDependencyMacro)

if(@USE_PYTHON@)
    find_dependency(Python @PYTHON_VERSION@)
    #find_dependency(Boost_PYTHON @Boost_VERSION@)
endif()

if(@USE_ROOT@)
    find_dependency(ROOT @ROOT_VERSION@)
endif()

if(NOT TARGET SNiPER::SniperKernel)
    include("${CMAKE_CURRENT_LIST_DIR}/SNiPERTargets.cmake")
endif()

check_required_components(SNiPER)

# Print the default "Found" message and check library location
include(FindPackageHandleStandardArgs)
get_property(TEST_SNIPER_LIBRARY TARGET SNiPER::SniperKernel PROPERTY LOCATION)
find_package_handle_standard_args(SNiPER DEFAULT_MSG CMAKE_CURRENT_LIST_FILE TEST_SNIPER_LIBRARY)

